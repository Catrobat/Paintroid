/**
 *  Paintroid: An image manipulation application for Android.
 *  Copyright (C) 2010-2022 The Catrobat Team
 *  (<http://developer.catrobat.org/credits>)
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as
 *  published by the Free Software Foundation, either version 3 of the
 *  License, or (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: "io.gitlab.arturbosch.detekt"

check.dependsOn 'checkstyle'
check.dependsOn 'pmd'
check.dependsOn 'detekt'

dependencies {
    pmd 'net.sourceforge.pmd:pmd-core:6.32.0'
    pmd 'net.sourceforge.pmd:pmd-java:6.32.0'

    checkstyle 'com.puppycrawl.tools:checkstyle:7.6'

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.20.0"
}

task checkstyle(type: Checkstyle) {
    configFile file('config/checkstyle.xml')
    source '.'
    include '**/*.java', '**/*.kt', '**/*.xml', '**/*.gradle'
    exclude '**/gen/**', '**/build/**', 'libraryProjects/**', '**/.idea/**'

    classpath = files()

    // needed for console output of warnings/errors
    showViolations true
    ignoreFailures false

    reports {
        xml.enabled = true
        xml.destination file("build/reports/checkstyle.xml")
    }
}

task pmd(type: Pmd) {
    ruleSetFiles = files('config/pmd.xml')
    ruleSets = []

    source '.'
    include '**/*.java'
    exclude '**/gen/**', '**/build/**', '**/res/**'

    ignoreFailures false

    reports {
        xml.enabled = true
        html.enabled = true
        xml.destination file("build/reports/pmd.xml")
    }
}

detekt {
    toolVersion = "1.20.0"
    config = files("$rootDir/Paintroid/config/detekt.yml")
    input = files("$rootDir/colorpicker/", "$rootDir/Paintroid/")
}
