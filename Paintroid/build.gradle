/**
 *  Paintroid: An image manipulation application for Android.
 *  Copyright (C) 2010-2022 The Catrobat Team
 *  (<http://developer.catrobat.org/credits>)
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as
 *  published by the Free Software Foundation, either version 3 of the
 *  License, or (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.hiya.jacoco-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'org.catrobat.gradle.androidemulators'
apply plugin: 'maven-publish'

apply from: 'gradle/adb_tasks.gradle'
apply from: 'gradle/code_quality_tasks.gradle'

emulators {
    install project.hasProperty('installSdk')

    dependencies {
        sdk()
    }

    emulator 'android28', {
        avd {
            systemImage = 'system-images;android-28;default;x86_64'
            sdcardSizeMb = 1024
            hardwareProperties += ['hw.ramSize': 4096, 'vm.heapSize': 1024]
            screenDensity = 'xhdpi'
        }

        parameters {
            resolution = '768x1280'
            language = 'en'
            country = 'US'
        }
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
    destination project.getBuildDir().getPath() + "/reports/jacoco/jacocoTestDebugUnitTestReport"
}

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    viewBinding {
        enabled = true
    }
    defaultConfig {
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        signedRelease {
        }
        debug {
            testCoverageEnabled = project.hasProperty('enableCoverage')
            // Multidex is required as espresso and mockito/bytebuddy are adding more functions
            // than should be allowed by law.
            // See https://github.com/mockito/mockito/issues/1112
            multiDexEnabled true
        }
    }

    lintOptions {
        // specific ignores should be defined via lint.xml file
        lintConfig file('config/lint.xml')
        ignore 'ClickableViewAccessibility', 'StaticFieldLeak', 'GradleDependency', 'OldTargetApi', 'LintBaseline'
        textReport true
        xmlReport true
        htmlReport true
        xmlOutput file("build/reports/lint-report.xml")
        htmlOutput file("build/reports/lint-report.html")
    }

    testOptions {
        unitTests.returnDefaultValues = true
        animationsDisabled = true
    }

    packagingOptions {
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation project(':colorpicker')

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'com.esotericsoftware:kryo:5.1.1'
    implementation 'id.zelory:compressor:2.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.databinding:databinding-runtime:7.3.0'

    debugImplementation 'androidx.multidex:multidex:2.0.0'

    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.jraska:falcon:2.2.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'

    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'org.mockito:mockito-android:2.18.3'
    androidTestImplementation 'tools.fastlane:screengrab:2.1.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0'
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.2.0"
    testImplementation "androidx.test:core-ktx:1.4.0"
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.1.0'
}

tasks.withType(Javadoc).all {
    enabled = false
}

if (project.hasProperty('jenkins')) {
    android.dexOptions.preDexLibraries = false
}
