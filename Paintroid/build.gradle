/**
 *  Paintroid: An image manipulation application for Android.
 *  Copyright (C) 2010-2015 The Catrobat Team
 *  (<http://developer.catrobat.org/credits>)
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as
 *  published by the Free Software Foundation, either version 3 of the
 *  License, or (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'org.catrobat.gradle.androidemulators'

emulators {
    install project.hasProperty('installSdk')

    dependencies {
        sdk()
    }

    emulator 'android24', {
        avd {
            systemImage = 'system-images;android-24;default;x86_64'
            sdcardSizeMb = 200
            hardwareProperties += ['hw.ramSize': 800, 'vm.heapSize': 128]
            screenDensity = 'xhdpi'
        }

        parameters {
            resolution = '768x1280'
            language = 'en'
            country = 'US'
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.vanniktech.android.apk.size'

apply from: 'gradle/adb_tasks.gradle'
apply from: 'gradle/code_quality_tasks.gradle'

check.dependsOn 'checkstyle'
check.dependsOn 'pmd'

jacoco {
    toolVersion = "0.8.1"
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        signedRelease {
        }
        debug {
            testCoverageEnabled = project.hasProperty('enableCoverage')
            // Multidex is required as espresso and mockito/bytebuddy are adding more functions
            // than should be allowed by law.
            // See https://github.com/mockito/mockito/issues/1112
            multiDexEnabled true
        }
    }

    lintOptions {
        // specific ignores should be defined via lint.xml file
        lintConfig file('config/lint.xml')
        ignore 'ClickableViewAccessibility', 'StaticFieldLeak', 'GradleDependency', 'OldTargetApi'

        textReport true
        xmlReport true
        htmlReport true
        xmlOutput file("build/reports/lint-report.xml")
        htmlOutput file("build/reports/lint-report.html")
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$androidSupportLibraryVersion"
    implementation "com.android.support:design:$androidSupportLibraryVersion"
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.11.0'
    implementation project(':colorpicker')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    debugImplementation 'com.android.support:multidex:1.0.3'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.mockito:mockito-android:2.18.3'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'

    pmd 'net.sourceforge.pmd:pmd-core:5.8.1'
    pmd 'net.sourceforge.pmd:pmd-java:5.8.1'

    checkstyle 'com.puppycrawl.tools:checkstyle:7.6'
}

tasks.withType(Javadoc).all {
    enabled = false
}

publish {
    userOrg = 'catrobat'
    repoName = 'Paintroid'
    groupId = 'org.catrobat.paintroid'
    artifactId = 'paintroid'
    publishVersion = rootProject.ext.androidVersionName
    licences = ['AGPL-V3']
    desc = 'A graphical paint editor library for Android'
    website = 'https://github.com/Catrobat/Paintroid'
    issueTracker = 'https://jira.catrob.at/browse/PAINTROID'
    repository = "${website}.git"
}

if (project.hasProperty('jenkins')) {
    android.dexOptions.preDexLibraries = false
}
